apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"
    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        applicationId "com.cardinfolink.yunshouyin"
        minSdkVersion 8
        targetSdkVersion 23
        versionCode 55
        versionName "2.0"
        buildConfigField 'String', 'URL', '"https://api.shou.money/app"'
        buildConfigField 'boolean', 'IS_PRODUCE', 'true'
    }

    productFlavors{
        cardinfolink {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "yunshouyin", ENVIRONMENT_VALUE: "pro"]
//            comment out below for go live
            buildConfigField 'String', 'URL', '"http://test.quick.ipay.so/app"'
            buildConfigField 'boolean', 'IS_PRODUCE', 'false'
        }
        yinxun {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "yinxun", ENVIRONMENT_VALUE: "pro"]
            //            comment out below for go live
            applicationId "com.cardinfolink.yunshouyin.yinxun"
            buildConfigField 'String', 'URL', '"http://test.quick.ipay.so/app"'
            buildConfigField 'boolean', 'IS_PRODUCE', 'false'
        }
        japan{
            applicationId "com.cardinfolink.yunshouyin.jp"
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "japan", ENVIRONMENT_VALUE: "pro"]
            versionName "0.5"
            versionCode 1
            //            comment out below for go live
            buildConfigField 'String', 'URL', '"http://test.quick.ipay.so/app"'
            buildConfigField 'boolean', 'IS_PRODUCE', 'false'
        }
    }

    sourceSets{
        cardinfolink.java.srcDirs = ['src/cardinfolink/java', 'src/main/java']
        yinxun.java.srcDirs = ['src/cardinfolink/java', 'src/main/java']
        japan.java.srcDirs = ['src/japan/java', 'src/main/java']
    }

    signingConfigs {
        debug {
        }

        release {
            storeFile file("../key.keystore")
            storePassword "cil1009"
            keyAlias "cardinfolink"
            keyPassword "cil1009"
        }
    }

    buildTypes {
        release {
//            TODO: 混淆代码
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
            shrinkResources true
            signingConfig signingConfigs.release
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        def fileName = "showmoney_v${variant.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }
}

dependencies {
    compile project(':cashierSdk')
    compile project(':library')
    compile project(':menulibrary')
    compile files('libs/commons-codec-1.3.jar')
    compile files('libs/core.jar')
    compile files('libs/umeng-analytics-v5.5.3.jar')
    compile files('libs/umeng-update-v2.6.0.1.jar')
}

def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
}